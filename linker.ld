OUTPUT_FORMAT(elf64-x86-64)
ENTRY(kmain)

PHDRS
{
	limine_requests PT_LOAD;
	text    	PT_LOAD;
	rodata  	PT_LOAD;
	data    	PT_LOAD;
}

SECTIONS
{ 
	. = 0xffffffff80000000;

	.limine_requests : {
		KEEP(*(.limine_requests_start))
		KEEP(*(.limine_requests))
		KEEP(*(.limine_requests_end))
	} :limine_requests

	. = ALIGN(CONSTANT(MAXPAGESIZE));

	.text : { 
		*(.text .text.*)
	} :text

	. = ALIGN(CONSTANT(MAXPAGESIZE));

	.rodata : { 
		*(.rodata .rodata*)
	} :rodata

	.note.gnu.build-id : {
		*(.note.gnu.build-id)
	} :rodata

	. = ALIGN(CONSTANT(MAXPAGESIZE));

	.data : {
		*(.data .data*)
	} :data

	. = ALIGN(CONSTANT(MAXPAGESIZE));

	.init_array : { 
		__init_array_start = .;
		KEEP(*(SORT_BY_INIT_PRIORITY(.init_array.*) SORT_BY_INIT_PRIORITY(.ctors.*)))
		KEEP(*(.init_array .ctors))
		__init_array_end = .;
	} :data

	. = ALIGN(CONSTANT(MAXPAGESIZE));

	.fini_array : {
		__fini_array_start = .;
		KEEP(*(SORT_BY_INIT_PRIORITY(.fini_array.*) SORT_BY_INIT_PRIORITY(.dtors.*)))
		KEEP(*(.fini_array .dtors))
		__fini_array_end = .;
	} :data

	.bss : {
		*(.bss .bss.*)
		*(COMMON)
	} :data

	/DISCARD/ : {
		*(.eh_frame*)
		*(.note .note.*)
	}
}
